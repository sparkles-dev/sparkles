plugins {
  id 'java'
  id 'application'
  id 'com.adarshr.test-logger'
  id 'com.palantir.git-version'
  id 'com.palantir.docker'
}

group = 'sparkles'
version = gitVersion()
description = 'A stuffing application that does stuff'

mainClassName = 'sparkles.StuffApp'
sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
  // 3rd party
  implementation 'com.google.guava:guava:27.0.1-jre'
  implementation 'io.javalin:javalin:2.8+'
  implementation 'io.jsonwebtoken:jjwt:0.9.1'
  implementation 'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final'
  implementation 'org.hibernate:hibernate-core:5.4.4.Final'
  implementation 'org.hsqldb:hsqldb:2.5.0'
  implementation 'org.slf4j:slf4j-api:1.7.27'
  implementation 'org.slf4j:slf4j-simple:1.7.27'
  implementation 'org.springframework.data:spring-data-commons:2.1.10.RELEASE'
  implementation 'org.springframework.data:spring-data-jpa:2.1.10.RELEASE'

  // project
  implementation project(':support:common')
  implementation project(':support:javalin:common')
  implementation project(':support:javalin:flyway')
  implementation project(':support:javalin:jwt')
  implementation project(':support:javalin:keycloak-security')
  implementation project(':support:javalin:spring-data')
  implementation project(':support:flyway')

  // testing
  testImplementation project(':support:javalin:testing')
  testImplementation 'com.squareup.okhttp3:okhttp:3.+'
  testImplementation 'io.javalin:javalin:2.8+'
  testImplementation 'junit:junit:4.+'
  testImplementation 'org.assertj:assertj-core:3.13.2'
}

docker {
  name 'sparkles/stuff'
  dockerfile file('../../tools/docker/gradle-app/Dockerfile')
  files tasks.distZip.outputs
  buildArgs([bin: 'stuff', zip: tasks.distZip.archiveName])
  pull true
}

run.doFirst {
  environment 'ENVIRONMENT', 'develop'
}

testlogger {
  theme 'mocha'
  showStandardStreams true
}
